dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.5)dnl
AC_INIT(gprof.c)

AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(gprof, 2.8.2)

AM_PROG_LIBTOOL

dnl For simplicity, we use the BFD configuration file for most
dnl things.  However, we also need our own configuration file for
dnl the automake PACKAGE and VERSION macros.  We don't name it
dnl config.h, to avoid any possible confusion with the bfd config.h.
AM_CONFIG_HEADER(gconfig.h:gconfig.in)

AC_PROG_CC
AM_PROG_INSTALL

AC_ISC_POSIX

case "${target}" in
alpha-*-*)	MY_TARGET=alpha ;;
changequote(,)dnl
i[3456]86-*-*)	MY_TARGET=i386 ;;
changequote([,])dnl
sparc-*-*)	MY_TARGET=sparc ;;
tahoe-*-*)	MY_TARGET=tahoe ;;
vax-*-*)	MY_TARGET=vax ;;
ns32k-*-*)	MY_TARGET=ns532;;
*-*-*)		MY_TARGET=dummy ;;
esac

AC_SUBST(MY_TARGET)

dnl We need to set BSD44_FORMAT on a host which uses the additional
dnl header fields.  This is an attempt to do that.  I can't think of a
dnl good feature test for this.
case "${target}" in
*-*-bsd4.4* | *-*-netbsd* | *-*-freebsd* | *-*-openbsd*)
  AC_DEFINE(BSD44_FORMAT)
  ;;
esac

AM_MAINTAINER_MODE
AM_CYGWIN32
AM_EXEEXT

AC_OUTPUT(Makefile)
